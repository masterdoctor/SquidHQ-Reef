import java.util.regex.Pattern

// Apply required plugins
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven-publish'

// Group and version information
group = "com.clout-team.sam"
version = gradle.squidVersion

// Repositories & dependencies
repositories {
    mavenCentral()

    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }

    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/public/"
    }

    maven {
        url "http://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'org.bukkit:bukkit:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.0.8'
}

// Task to replace version number in plugin.yml
//noinspection GroovyAssignabilityCheck
task finalize << {
    File pluginYaml = new File( 'src/resources/plugin.yml' )
    String contents = pluginYaml.getText( 'UTF-8' )

    contents = contents.replaceAll( Pattern.quote("{gradle-name}"), "${gradle.pluginName}" )
    contents = contents.replaceAll( Pattern.quote("{gradle-version}"), "${project.version}" )
    contents = contents.replaceAll( Pattern.quote("{gradle-author}"), "${gradle.squidAuthor}" )

    new File( 'build/resources/main/plugin.yml' ).write( contents, 'UTF-8' )

    println "Patched plugin.yml"
}

// Perform the plugin.yml replace code whilst processing resources
processResources.finalizedBy(finalize)

/* `gradle build` from the command line */
sourceSets {
    main {
        java {
            srcDirs = ['src/java']
        }

        resources {
            srcDirs = ['src/resources']
        }
    }
}

// Custom archive name
jar {
    archiveName "SquidHQ-${version}.jar"
}

/* `gradle publish` from the command line */
publishing {
    publications {
        mavenJava(MavenPublication) {
            version "${version}"

            from components.java
        }
    }

    repositories {
        maven {
            credentials {
                username ''
                password ''
            }
            url "http://nexus.clout-team.com/repository/clout-team/"
        }
    }
}